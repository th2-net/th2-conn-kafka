plugins {
    id 'application'
    id 'com.palantir.docker' version '0.25.0'
    id 'org.jetbrains.kotlin.jvm' version "${kotlin_version}"
}

group 'com.exactpro.th2'
version release_version

sourceCompatibility = 11
targetCompatibility = 11

repositories {
    mavenCentral()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

jar {
    manifest {
        attributes(
                'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title':      '',
                'Specification-Vendor':     'Exactpro Systems LLC',
                'Implementation-Title':     project.archivesBaseName,
                'Implementation-Vendor':    'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version':   project.version
        )
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:4.0.2')
    implementation 'org.slf4j:slf4j-log4j12'
    implementation 'org.slf4j:slf4j-api'

    implementation 'com.exactpro.th2:common:5.0.0-dev-version-5-3838510969-SNAPSHOT'

    implementation 'io.reactivex.rxjava3:rxkotlin:3.0.1'
    implementation 'org.apache.kafka:kafka_2.13:3.3.1'
    implementation 'io.github.microutils:kotlin-logging:3.0.4'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
}

test {
    useJUnitPlatform()
}

applicationName = 'service'

application {
    mainClassName 'com.exactpro.th2.kafka.client.Main'
}

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}
